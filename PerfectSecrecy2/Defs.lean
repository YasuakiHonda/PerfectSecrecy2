/-
Copyright (c) 2025 Yasuaki Honda. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yasuaki Honda
-/

import Mathlib.Probability.ProbabilityMassFunction.Monad

namespace PerfectSecrecy

open PMF

variable {M K C : Type}


/-- Correctness property of symmetric encryption.
Decrypting an encrypted plain text generates the plain text.
Dec(k, Enc(k, m))=m
-/
def correctness (Enc : K → M → C) (Dec : K → C → M) :=
  ∀ k:K, ∀ m:M, Dec k (Enc k m) = m

/-- Given a plain text m, the distribution of the cipher text generated by
encrypting m with a key generated by Gen.
Pr[C=c] = Pr_k←Gen [C=Enc(k,m)]
-/
noncomputable
def Enc_dist (Enc : K → M → C) (Gen : PMF K) (m : M) : PMF C :=
  do
    let k ← Gen
    PMF.pure (Enc k m)

/-- Distribution of cipher text where the message is generated according to Msg
and the key is generated according to Gen.
Pr[C=c] = Pr_m←Msg [C=Enc(k,m)] where k←Gen
-/
noncomputable
def cipher_dist (Enc : K → M → C) (Gen : PMF K) (Msg : PMF M) : PMF C := do
  let m ← Msg
  Enc_dist Enc Gen m

/-- Indistinguishability-based definition of perfect secrecy.
A symmetric encryption scheme (Enc, Dec, Gen) is said to achieve perfect secrecy if
for every pair of messages m1, m2 ∈ M and every cipher text c ∈ C, the probability of c
being the encryption of m1 is equal to the probability of c being the encryption of m2,
when the key is generated according to Gen. -/
def ind_perfect_secrecy (Enc : K → M → C) (Gen : PMF K) :=
  ∀ (m1 m2 : M) (c : C),
    (Enc_dist Enc Gen m1) c = (Enc_dist Enc Gen m2) c

/-- Indistinguishability-based definition of perfect secrecy for message distributions.
A symmetric encryption scheme (Enc, Dec, Gen) is said to achieve perfect secrecy if
for every pair of message distributions Msg1, Msg2 over M and every cipher text c ∈ C,
the probability of c being the encryption of a message generated according to Msg1
is equal to the probability of c being the encryption of a message generated according to Msg2,
when the key is generated according to Gen. -/
def ind_prior_perfect_secrecy (Enc : K → M → C) (Gen : PMF K) :=
  ∀ (Msg1 Msg2 : PMF M) (c : C),
    (cipher_dist Enc Gen Msg1) c = (cipher_dist Enc Gen Msg2) c

/-- Joint distribution of message and cipher text where the message is generated
according to Msg and the key is generated according to Gen.
Pr[M=m ∧ C=c] = Pr_k←Gen [M=m ∧ C=Enc(k,m)]
-/
noncomputable
def joint_dist (Enc : K → M → C) (Gen : PMF K) (Msg : PMF M) : PMF (M × C) :=
  do
    let m ← Msg
    let k ← Gen
    PMF.pure (m, Enc k m)

/-- Posterior distribution of message m given cipher text c.
Pr[M=m|C=c] = Pr[M=m ∧ C=c] / Pr[C=c] -/
noncomputable
def posterior (Enc : K → M → C) (Gen : PMF K) (Msg : PMF M) (m : M) (c : C) : ENNReal :=
  let joint := joint_dist Enc Gen Msg
  if (cipher_dist Enc Gen Msg) c ≠ 0 then
    (joint (m , c)) / ((cipher_dist Enc Gen Msg) c)
  else 0

/-- Shannon's original definition of perfect secrecy.
A symmetric encryption scheme (Enc, Dec, Gen) is said to achieve perfect secrecy if
for every message distribution Msg over M, every message m ∈ M, and every cipher text c ∈ C
with non-zero probability of being generated, the posterior probability of m given c
is equal to the prior probability of m.
Pr[M=m|C=c] = Pr[M=m]
-/
def shannon_perfect_secrecy (Enc : K → M → C) (Gen : PMF K) : Prop :=
  ∀ (Msg : PMF M) (m : M) (c : C),
    (cipher_dist Enc Gen Msg) c ≠ 0 → posterior Enc Gen Msg m c = Msg m


end PerfectSecrecy
